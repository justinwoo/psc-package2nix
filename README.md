# Psc-Package2Nix

Tool to derive a Nix expression from a `psc-package.json` configuration.

Prefetches Git SHA of the total dependencies you depend on.

## Usage

In the [test](./test) setup, we have the `packages.nix` that is generated by `psc-package2nix` checked in, so you can see what kind of file we are generating here.

Then when you look at `default.nix`, you will find that we have a normal derivation, where...

First, we use [easy-purescript-nix](https://github.com/justinwoo/easy-purescript-nix) to easily install the PureScript compiler and Psc-Package (see `buildInputs` below).

Second, we bring in `psc-package2nix` from this repository remotely. This expression takes an argument should you wish to override `pkgs`, but this will default to using `<nixpkgs>`.

Then, we bring in the packages that have been generated, and create a list using the attribute values of the set (reminder: records are called "attribute sets" in Nixlang).

With this, we then prepare for the Psc-Package dependencies to be copied from the Nix Store, which will give us local mutable copies in `.psc-package/`.

Then we can complete the `buildInputs` with the derivations we need for our derivation, along with a shell hook that will copy the dependencies over.

```nix
let
  pkgs = import <nixpkgs> {};

  easy-ps = import (pkgs.fetchFromGitHub {
    owner = "justinwoo";
    repo = "easy-purescript-nix";
    rev = "347ab7c91634462c2039c6c0641af5386c251a98";
    sha256 = "0njhcl7dq58b3kmjbz6ndsccv4pcmdxc5lg7p13115phcmznpn99";
  });

  psc-package2nix = import (pkgs.fetchFromGitHub {
    owner = "justinwoo";
    repo = "psc-package2nix";
    rev = "edaf492e939decfebf3d0ab6b9f04ec1c317e028";
    sha256 = "06p1ggk38hmfd9461l4w822gavz7kizx05v9r2lb22qdsg3m94gd";
  }) {};

  packages = import ./packages.nix {};
  packageDrvs = builtins.attrValues packages.inputs;

  pathsToCopy = map (x: toString x.outPath) packageDrvs;
  copyCmds = map (x: let target = ".psc-package/${packages.set}/${x.name}/${x.version}"; in ''
    mkdir -p ${target}
    cp --no-preserve=mode,ownership,timestamp -r ${toString x.outPath}/* ${target}
  '') packageDrvs;

in pkgs.stdenv.mkDerivation {
  name = "test";
  src = ./.;

  buildInputs
    = packageDrvs
    ++ [
      easy-ps.inputs.purs
      easy-ps.inputs.psc-package-simple
      psc-package2nix
    ];

  shellHook = ''
    mkdir -p .psc-package
  '' + toString copyCmds;
}
```

Of course, you might use the full power of the Nix language to tweak this to fit your uses.

## More Information

If you need more information on how Psc-Package works, you might see the Spacchetti guide page here: <https://spacchetti.readthedocs.io/en/latest/intro.html>
